abstract type Aspect end
struct Maximum <: Aspect end
struct Minimum <: Aspect end
struct Mean <: Aspect end
struct DiurnalRange <: Aspect end
struct DailyTotal <: Aspect end

const Qualifier = Union{Nothing, Aspect, Microclimate.TimeOfDay}

abstract type Sample end

# Each physical quantity a sample can carry: (type name, physical_quantity key,
# working unit). A `nothing` unit is dimensionless. The struct, its zero-arg
# constructor, `physical_quantity`, and `working_unit` are all generated from
# this one table.
const _SAMPLE_QUANTITIES = (
    (:Temperature, :temperature, u"K"),
    (:WindSpeed, :wind_speed, u"m/s"),
    (:EastwardWindSpeed, :eastward_wind, u"m/s"),
    (:NorthwardWindSpeed, :northward_wind, u"m/s"),
    (:RelativeHumidity, :humidity, nothing),
    (:GlobalRadiation, :global_radiation, u"W/m^2"),
    (:LongwaveRadiation, :longwave_radiation, u"W/m^2"),
    (:Rainfall, :rainfall, u"kg/m^2"),
    (:CloudCover, :cloud_cover, nothing),
    (:DewpointTemperature, :dewpoint_temperature, u"K"),
    (:Pressure, :pressure, u"Pa"),
    (:SpecificHumidity, :specific_humidity, nothing),
    (:VapourPressureDeficit, :vapour_pressure_deficit, u"kPa"),
    (:SoilMoisture, :soil_moisture, nothing),
    (:ActualVapourPressure, :actual_vapour_pressure, u"kPa"),
    (:SoilTemperature, :soil_temperature, u"K"),
    (:ZenithAngle, :zenith_angle, u"°"),
    (:Elevation, :elevation, u"m"),
)

for (Q, pq, unit) in _SAMPLE_QUANTITIES
    @eval begin
        struct $Q{Q<:Qualifier} <: Sample
            at::Q
        end
        $Q() = $Q(nothing)
        @inline physical_quantity(::$Q) = $(QuoteNode(pq))
        @inline working_unit(::$Q) = $unit
    end
end

struct Reference{S<:Sample} <: Sample
    sample::S
end

@inline qualifier(q::Sample) = q.at
@inline qualifier(r::Reference) = qualifier(r.sample)

@inline working_unit(r::Reference) = working_unit(r.sample)
@inline physical_quantity(r::Reference) = physical_quantity(r.sample)

@inline canonical_name(q::Sample) = _canonical_name(q, qualifier(q))
@inline _canonical_name(q::Sample, ::Nothing) = physical_quantity(q)
@inline _canonical_name(q::Sample, ::Maximum) = Symbol(physical_quantity(q), :_max)
@inline _canonical_name(q::Sample, ::Minimum) = Symbol(physical_quantity(q), :_min)
@inline _canonical_name(q::Sample, ::Mean) = Symbol(:mean_, physical_quantity(q))
@inline _canonical_name(q::Sample, ::DiurnalRange) = Symbol(:diurnal_, physical_quantity(q), :_range)
@inline _canonical_name(q::Sample, ::DailyTotal) = Symbol(physical_quantity(q), :_daily)
@inline _canonical_name(q::Sample, t::Microclimate.TimeOfDay) =
    _timed_name(physical_quantity(q), t)
@inline canonical_name(::SoilTemperature{Mean}) = :deep_soil_temperature
@inline canonical_name(r::Reference) = Symbol(:reference_, canonical_name(r.sample))

Base.@assume_effects :foldable function _timed_name(base::Symbol, t::Microclimate.ClockTime)
    h = t.hour
    tag = isinteger(h) ? string(Int(h)) : replace(string(h), "." => "_")
    return Symbol(base, :_, tag)
end

struct Variable{Name, Field, Q, U, T}
    quantity::Q
    unit::U
    transform::T
end
function Variable(quantity::Sample, field::Symbol, unit = 1, transform = identity)
    name = canonical_name(quantity)
    return Variable{name, field, typeof(quantity), typeof(unit), typeof(transform)}(
        quantity, unit, transform)
end

@inline canonical_name(::Variable{Name}) where {Name} = Name
@inline native_field(::Variable{<:Any, Field}) where {Field} = Field
@inline quantity(v::Variable) = v.quantity

abstract type Calendar end
struct Monthly <: Calendar end
struct Daily <: Calendar end

abstract type Timestep end
struct MinMax <: Timestep end
struct SubDaily{N} <: Timestep end
const Hourly = SubDaily{24}
const SixHourly = SubDaily{4}

@inline samples_per_day(::SubDaily{N}) where {N} = N
@inline samples_per_day(::MinMax) = 1

@inline weather_calendar(::Type) = Monthly()

@inline native_timestep(::Type) = MinMax()

@inline days_per_year(::Monthly) = 12
@inline days_per_year(::Daily) = 365

@inline steps_per_year(cal::Calendar, cad::Timestep) =
    days_per_year(cal) * samples_per_day(cad)

# ---------------------------------------------------------------------------
# Loader traits
# ---------------------------------------------------------------------------
abstract type Loader end

struct YearlyTimeSeries <: Loader end
struct MonthlyClimatology <: Loader end
struct MonthlyClimatologyPeriod <: Loader end
struct MultiBandClimatologyPeriod <: Loader end
struct SingleFileBands <: Loader end
struct DailyFiles <: Loader end
struct ContiguousTimeSeries <: Loader end
function loader end

abstract type LongitudeConvention end
struct Longitude180 <: LongitudeConvention end
struct Longitude360 <: LongitudeConvention end

@inline longitude_convention(::Type) = Longitude180()

@inline _native_lon_crop(::Longitude180, area::Extent) = (area, identity)
function _native_lon_crop(::Longitude360, area::Extent)
    (area.X[1] >= 0 && area.X[2] >= 0) && return (area, identity)
    load_area = Extent(X = (mod(area.X[1], 360.0), mod(area.X[2], 360.0)), Y = area.Y)
    return (load_area, spatial_dims -> _shift_x_lookup(spatial_dims, -360.0))
end

function _shift_x_lookup(spatial_dims, by)
    x_lk = lookup(spatial_dims, X)
    new_x = X(Sampled(collect(x_lk) .+ by;
        order = order(x_lk), span = span(x_lk), sampling = sampling(x_lk)))
    return (new_x, dims(spatial_dims, Y))
end

# ---------------------------------------------------------------------------
# Per-source extension points (with defaults)
# ---------------------------------------------------------------------------

@inline layers(source) = map(canonical_name, variables(source))
@inline fallback_source(::Type) = nothing
# A source's fallback layers are exactly the baseline's layers it does not
# provide itself, so they follow from `variables` and `fallback_source`.
@inline fallback_layers(source) = _fallback_layers(source, fallback_source(source))
@inline _fallback_layers(_source, ::Nothing) = ()
@inline _fallback_layers(source, baseline) = _names_absent(layers(source), layers(baseline))
@inline _names_absent(_own, ::Tuple{}) = ()
@inline _names_absent(own, candidates::Tuple) =
    first(candidates) in own ?
        _names_absent(own, Base.tail(candidates)) :
        (first(candidates), _names_absent(own, Base.tail(candidates))...)
@inline _extra_getraster_kwargs(::Type) = (;)

# A source's grid elevation is just its declared `Elevation()` quantity, read at
# the first timestep (it is static across Ti). Sources that don't declare it get
# `nothing` and fall back to the site DEM elevation.
@inline weather_grid_elevation(source::Type, weather, I) =
    _grid_elevation(_maybe_variable(variables(source), :elevation), weather, I)
@inline _grid_elevation(::Nothing, _weather, _I) = nothing
@inline _grid_elevation(var::Variable, weather, I) =
    u"m"(var.transform(weather[:elevation][I..., Ti(1)]) * var.unit)

function _aggregate_ti_to_daily(layer::AbstractRaster, factor::Int)
    return Rasters.aggregate(mean, layer, (Ti(factor),))
end

function _load_weather(source::Type, area::Extent, years)
    primary_stack = _load_canonical(source, layers(source), area, years)
    baseline = fallback_source(source)
    stack = baseline === nothing ?
        RasterStack(primary_stack) :
        RasterStack(merge(primary_stack,
            _load_canonical(baseline, fallback_layers(source), area, years)))
    # Source files declare a `missingval`, so loaded eltypes are
    # `Union{Missing, T}`. The per-pixel reader does `value * unit`,
    # which throws `convert(Missing, Quantity)` on any masked cell.
    # `replace_missing(..., NaN)` strips the Missing union and lets
    # NaN propagate visibly if any cell is genuinely masked.
    stack = Rasters.replace_missing(stack, NaN)
    # Every source must deliver exactly the native step count it declares
    # (calendar days/year × samples/day × years). Catch any loader that
    # returns a different Ti rather than mis-slicing it downstream.
    expected = steps_per_year(weather_calendar(source), native_timestep(source)) * length(years)
    n = length(dims(stack, Ti))
    n == expected || error(
        "$(nameof(source)): loaded Ti=$n, but the source declares $expected " *
        "($(nameof(typeof(weather_calendar(source)))) × " *
        "$(nameof(typeof(native_timestep(source)))), $(length(years)) years)")
    return stack
end

# ---------------------------------------------------------------------------
# Per-loader file-reading
# ---------------------------------------------------------------------------

function _load_layers(::YearlyTimeSeries, source, fields::Tuple, area::Extent, years)
    extras = _extra_getraster_kwargs(source)
    layers = map(fields) do name
        per_year = map(years) do yr
            path = getraster(source, name; date = Date(yr), extras...)
            read(crop(Raster(path; lazy = true); to = area, touches = true))
        end
        cat(per_year...; dims = Ti)
    end
    return NamedTuple{fields}(layers)
end
function _load_layers(::MonthlyClimatology, source, fields::Tuple, area::Extent, years)
    nyears = length(years)
    layers = map(fields) do name
        _build_monthly_climatology(area, nyears) do month
            getraster(source, name; month)
        end
    end
    return NamedTuple{fields}(layers)
end
function _load_layers(::MonthlyClimatologyPeriod, source, fields::Tuple, area::Extent, years)
    nyears = length(years)
    period_date = Date(first(years))
    layers = map(fields) do name
        _build_monthly_climatology(area, nyears) do month
            getraster(source, name; date = period_date, month)
        end
    end
    return NamedTuple{fields}(layers)
end
function _load_layers(::MultiBandClimatologyPeriod, source, fields::Tuple,
                      area::Extent, years)
    nyears = length(years)
    period_date = Date(first(years))
    layers = map(fields) do name
        path = getraster(source, name; date = period_date)
        full = read(crop(Raster(path; lazy = true); to = area, touches = true))
        # File holds 12 months along the 3rd dim (Band or Ti, depending on
        # the format). Pull out the data array, tile across years, and re-
        # wrap with a proper `Ti` axis.
        data = parent(full)
        size(data, 3) == 12 || error(
            "$source: expected a 12-month multi-band file for $name, got $(size(data, 3)) bands")
        tiled = nyears == 1 ? data : repeat(data; outer = (1, 1, nyears))
        spatial_dims = dims(full)[1:2]
        Raster(tiled, (spatial_dims..., Ti(1:(12 * nyears))); crs = crs(full))
    end
    return NamedTuple{fields}(layers)
end
function _load_layers(::SingleFileBands, source, fields::Tuple, area::Extent, years)
    nyears = length(years)
    load_area, restore_dims = _native_lon_crop(longitude_convention(source), area)
    path = getraster(source; _extra_getraster_kwargs(source)...)
    raw = read(crop(RasterStack(path; name = fields, lazy = true);
                    to = load_area, touches = true))
    # Infer native step count from the first 3-D layer (some layers, e.g. CRUCL2 :elv, are static 2-D).
    nsteps = 1
    for name in fields
        ndims(raw[name]) >= 3 && (nsteps = size(parent(raw[name]), 3); break)
    end
    ntotal = nsteps * nyears
    layers = map(fields) do name
        lyr  = raw[name]
        data = parent(lyr)
        tiled = if ndims(data) == 2
            repeat(reshape(data, size(data)..., 1); outer = (1, 1, ntotal))
        else
            nyears == 1 ? data : repeat(data; outer = (1, 1, nyears))
        end
        Raster(tiled, (restore_dims(dims(lyr)[1:2])..., Ti(1:ntotal)); crs = crs(lyr))
    end
    return NamedTuple{fields}(layers)
end
function _load_layers(::DailyFiles, source, fields::Tuple, area::Extent, years)
    extras = _extra_getraster_kwargs(source)
    dates = _daily_date_sequence(years)
    nsteps = length(dates)
    layers = map(fields) do name
        per_day = map(dates) do d
            path = getraster(source, name; date = d, extras...)
            read(crop(Raster(path; lazy = true); to = area, touches = true))
        end
        first_day = first(per_day)
        spatial_dims = dims(first_day)
        stacked = cat(map(parent, per_day)...; dims = 3)
        Raster(stacked, (spatial_dims..., Ti(1:nsteps)); crs = crs(first_day))
    end
    return NamedTuple{fields}(layers)
end
function _load_layers(::ContiguousTimeSeries, source, fields::Tuple, area::Extent, years)
    cloud_source = getraster(source)
    full_stack = RasterStack(cloud_source.url; source = Rasters.Zarrsource(), lazy = true)
    time_start = DateTime(first(years), 1, 1, 0)
    time_end = DateTime(last(years), 12, 31, 23)
    layers = map(fields) do name
        long_name = layername(source, name)
        raw = getproperty(full_stack, Symbol(long_name))
        read(view(raw,
            X(area.X[1] .. area.X[2]),
            Y(area.Y[1] .. area.Y[2]),
            Ti(time_start .. time_end),
        ))
    end
    return NamedTuple{fields}(layers)
end

function _daily_date_sequence(years)
    dates = Date[]
    for y in years
        for d in Date(y, 1, 1):Day(1):Date(y, 12, 31)
            (Dates.month(d) == 2 && Dates.day(d) == 29) && continue
            push!(dates, d)
        end
    end
    return dates
end


function _build_monthly_climatology(path_for, area::Extent, nyears::Int)
    monthly = map(1:12) do m
        path = path_for(m)
        read(crop(Raster(path; lazy = true); to = area, touches = true))
    end
    first_monthly = first(monthly)
    spatial_dims = dims(first_monthly)
    climatology = cat(map(parent, monthly)...; dims = 3)
    tiled = nyears == 1 ? climatology :
                                  repeat(climatology; outer = (1, 1, nyears))
    return Raster(tiled, (spatial_dims..., Ti(1:(12 * nyears))); crs = crs(first_monthly))
end

@inline _unique_native_fields(vars::Tuple) = _unique_native_fields((), vars)
@inline _unique_native_fields(acc::Tuple, ::Tuple{}) = acc
@inline function _unique_native_fields(acc::Tuple, vars::Tuple)
    f = native_field(first(vars))
    new_acc = _contains(acc, f) ? acc : (acc..., f)
    return _unique_native_fields(new_acc, Base.tail(vars))
end
@inline _contains(::Tuple{}, _) = false
@inline _contains(t::Tuple, f) = first(t) === f || _contains(Base.tail(t), f)

function _load_canonical(source, names::Tuple, area::Extent, years)
    vars = _select_variables(variables(source), names)
    native_stack = _load_layers(loader(source), source,
                                _unique_native_fields(vars), area, years)
    return _canonical_keyed(vars, native_stack)
end

@inline _select_variables(vars::Tuple, names::Tuple) =
    map(name -> _variable_for(vars, name), names)
@inline _variable_for(::Tuple{}, name) =
    error("no weather variable declares the canonical name :$name")
@inline _variable_for(vars::Tuple, name) =
    canonical_name(first(vars)) === name ? first(vars) :
    _variable_for(Base.tail(vars), name)

@inline _maybe_variable(::Tuple{}, _name) = nothing
@inline _maybe_variable(vars::Tuple, name) =
    canonical_name(first(vars)) === name ? first(vars) :
    _maybe_variable(Base.tail(vars), name)

@inline _canonical_keyed(vars::Tuple, native_stack) =
    NamedTuple{map(canonical_name, vars)}(
        map(v -> getproperty(native_stack, native_field(v)), vars))

function _load_prescribed(source, sample::Sample, area::Extent, years)
    name = canonical_name(sample)
    raw = Rasters.replace_missing(_load_canonical(source, (name,), area, years)[name], NaN)
    var = _variable_for(variables(source), name)
    converted = ustrip.(canonical_unit(name), var.transform.(parent(raw)) .* var.unit)
    return Raster(converted, dims(raw); crs = crs(raw))
end

# ---------------------------------------------------------------------------
# Derivation registry
# ---------------------------------------------------------------------------

# Previously
# const _ENVELOPE_PHYSICS = (
#     Val(:reference_temperature_max),
#     Val(:reference_temperature_min),
#     Val(:mean_temperature),
#     Val(:wind_speed),
#     Val(:actual_vapour_pressure),
#     Val(:vapour_pressure_deficit),
#     Val(:reference_humidity_max),
#     Val(:reference_humidity_min),
#     Val(:reference_wind_max),
#     Val(:reference_wind_min),
#     Val(:cloud_cover),
#     Val(:cloud_min),
#     Val(:cloud_max),
#     Val(:deep_soil_temperature),
# )

const _ENVELOPE_PHYSICS = (
    Temperature(Maximum()),
    Temperature(Minimum()),
    Reference(Temperature(Maximum())),
    Reference(Temperature(Minimum())),
    Temperature(Mean()),
    WindSpeed(),
    ActualVapourPressure(),
    VapourPressureDeficit(),
    Reference(RelativeHumidity(Maximum())),
    Reference(RelativeHumidity(Minimum())),
    Reference(WindSpeed(Maximum())),
    Reference(WindSpeed(Minimum())),
    CloudCover(),
    CloudCover(Minimum()),
    CloudCover(Maximum()),
    SoilTemperature(Mean()),
)

# Previously
# const _NATIVE_PHYSICS = (
#     Val(:solar_geometry),
#     Val(:wind_speed),
#     Val(:actual_vapour_pressure),
# )

const _NATIVE_PHYSICS = (
   WindSpeed(),
   ActualVapourPressure(),
)

const _OUTPUT_PHYSICS = (
    Reference(WindSpeed()),
    Reference(RelativeHumidity()),
    SoilTemperature(Mean()),
    Rainfall(DailyTotal()),
)

abstract type ResamplingRule end
struct Interpolate <: ResamplingRule end
struct Disaggregate <: ResamplingRule end
struct Accumulate <: ResamplingRule end

# TODO these dont seem right - how is there enough information just from the kind to know
# the required resampling
@inline resampling_rule(::Sample) = Interpolate()
@inline resampling_rule(::GlobalRadiation) = Disaggregate()
@inline resampling_rule(::Rainfall) = Accumulate()
@inline resampling_rule(r::Reference) = resampling_rule(r.sample)

@inline function _native_at(src, d, i, nin, ndays)
    if i < 0
        d > 1 ? src[(d - 2) * nin + nin] : src[(d - 1) * nin + 1]
    elseif i >= nin
        d < ndays ? src[d * nin + 1] : src[(d - 1) * nin + nin]
    else
        src[(d - 1) * nin + i + 1]
    end
end

function _resample!(out, src, ::Interpolate, nin::Int, nout::Int)
    ndays = length(src) ÷ nin
    @inbounds for d in 1:ndays
        ob = (d - 1) * nout
        for j in 0:(nout - 1)
            x  = ((j + 0.5) / nout) * nin - 0.5   # output midpoint on native index axis
            i0 = floor(Int, x)
            t  = x - i0
            v0 = _native_at(src, d, i0,     nin, ndays)
            v1 = _native_at(src, d, i0 + 1, nin, ndays)
            out[ob + j + 1] = v0 * (1 - t) + v1 * t
        end
    end
    return out
end
function _resample!(out, src, ::Disaggregate, nin::Int, nout::Int, clear_sky)
    ndays = length(src) ÷ nin
    hpb = nout ÷ nin                       # output steps per native block
    @inbounds for d in 1:ndays
        hb = (d - 1) * nout
        for b in 0:(nin - 1)
            cs_sum = zero(eltype(clear_sky))
            for j in 1:hpb
                cs_sum += clear_sky[hb + b * hpb + j]
            end
            cs_mean = cs_sum / hpb
            obs = src[(d - 1) * nin + b + 1]
            opacity = cs_mean <= zero(cs_mean) ? 0.0 :
                clamp(ustrip(u"W/m^2", obs) / ustrip(u"W/m^2", cs_mean), 0.0, 1.0)
            for j in 1:hpb
                out[hb + b * hpb + j] = opacity * clear_sky[hb + b * hpb + j]
            end
        end
    end
    return out
end
function _resample!(daily, src, ::Accumulate, nin::Int)
    ndays = length(daily)
    @inbounds for d in 1:ndays
        s = zero(eltype(src))
        for i in 1:nin
            s += src[(d - 1) * nin + i]
        end
        daily[d] = s
    end
    return daily
end

@inline function _resample_to_output!(buffers, nin::Int, nout::Int, ctx)
    native = buffers.native
    map(_SERIES_OUTPUT) do q
        _resample_quantity!(buffers, native, q, nin, nout, ctx)
    end
    return nothing
end

@inline function _resample_quantity!(buffers, native, q::Sample, nin, nout, ctx)
    name = canonical_name(q)
    hasproperty(native, name) || return nothing
    _resample_by_rule!(getproperty(buffers, name), getproperty(native, name),
                       resampling_rule(q), nin, nout, ctx)
    return nothing
end

@inline function _resample_by_rule!(out, src, rule::Union{Interpolate,Disaggregate},
                                    nin, nout, ctx)
    nin == nout ? copyto!(out, src) : _disaggregate!(out, src, rule, nin, nout, ctx)
    return nothing
end
@inline _disaggregate!(out, src, rule::Interpolate, nin, nout, ctx) =
    _resample!(out, src, rule, nin, nout)
@inline _disaggregate!(out, src, rule::Disaggregate, nin, nout, ctx) =
    _resample!(out, src, rule, nin, nout, ctx.scratch.solar.out.global_horizontal)
@inline _resample_by_rule!(out, src, ::Accumulate, nin, nout, ctx) = nothing


@inline function _wind_speed!(out, u, v)
    @. out = sqrt(u^2 + v^2)
    return out
end

function _vapour_pressure_from_specific_humidity!(out, q, p)
    @inbounds for k in eachindex(out)
        out[k] = q[k] * p[k] / (0.622 + 0.378 * q[k])
    end
    return out
end

function _deep_soil!(out, series, out_per_year)
    nyears = max(1, length(out) ÷ out_per_year)
    out_per = length(out) ÷ nyears
    series_per = length(series) ÷ nyears
    @inbounds for y in 1:nyears
        s = zero(eltype(series))
        sb = (y - 1) * series_per
        for k in 1:series_per
            s += series[sb + k]
        end
        m = s / series_per
        ob = (y - 1) * out_per
        for d in 1:out_per
            out[ob + d] = m
        end
    end
    return out
end

# ---------------------------------------------------------------------------
# Buffer allocation
# ---------------------------------------------------------------------------

function allocate_weather_buffers(source::Type, target::Timestep, nyears::Int)
    cal = weather_calendar(source)
    native = native_timestep(source)
    _allocate_weather_buffers(cal, native, target, source, _days_of_year(cal, nyears))
end

@inline _native_quantities(source::Type) =
    (map(quantity, variables(source))..., WindSpeed(), ActualVapourPressure())

@inline _zeros(unit, n) = zeros(typeof(0.0 * unit), n)
@inline _zeros(::Nothing, n) = zeros(Float64, n)

@inline _zero_buffers(quantities::Tuple, n) =
    NamedTuple{map(canonical_name, quantities)}(map(q -> _zeros(working_unit(q), n), quantities))

function _environment_daily(n, rainfall, deep_soil_temperature)
    DailyTimeseries(;
        shade = zeros(Float64, n),
        soil_wetness = zeros(Float64, n),
        surface_emissivity = fill(0.95, n),
        cloud_emissivity = fill(0.95, n),
        rainfall, deep_soil_temperature,
        leaf_area_index = fill(0.1, n),
    )
end

_environment_hourly(out) = HourlyTimeseries(;
    out.pressure, out.reference_temperature, out.reference_humidity,
    reference_wind_speed = out.wind_speed,
    out.global_radiation, out.longwave_radiation, out.cloud_cover,
    out.rainfall, out.zenith_angle,
)

_environment_hourly_stub(n) = HourlyTimeseries(;
    pressure = Fill(atmospheric_pressure(0.0u"m"), n * 24),
    reference_temperature = nothing, reference_humidity = nothing,
    reference_wind_speed = nothing, global_radiation = nothing,
    longwave_radiation = nothing, cloud_cover = nothing,
    rainfall = nothing, zenith_angle = nothing,
)

const _ENVELOPE_BUFFERS = (
    Temperature(Maximum()), Temperature(Minimum()), Temperature(Mean()),
    Reference(Temperature(Minimum())), Reference(Temperature(Maximum())),
    WindSpeed(), EastwardWindSpeed(), NorthwardWindSpeed(),
    Reference(WindSpeed(Minimum())), Reference(WindSpeed(Maximum())),
    VapourPressureDeficit(),
    ActualVapourPressure(),
    SpecificHumidity(), Pressure(),
    Reference(RelativeHumidity(Minimum())), Reference(RelativeHumidity(Maximum())),
    GlobalRadiation(), CloudCover(), CloudCover(Minimum()), CloudCover(Maximum()),
    Rainfall(), SoilTemperature(Mean()), SoilMoisture(),
)

function _envelope_forcings(variables::Tuple, b)
    valuesource = name -> getproperty(b, name)
    standard = Microclimate.bind_forcings(Microclimate.MINMAX_FORCING_MODEL, valuesource)
    timed = _timed_forcings(variables, valuesource)
    isempty(timed) && return standard
    base = (; standard.reference_temperature, standard.reference_wind_speed, standard.cloud_cover)
    humidity = haskey(timed, :actual_vapour_pressure) ?
        (; reference_humidity = Derived(
            RelativeHumidityFromVapourPressureAndTemperature(GoffGratch()),
            (:actual_vapour_pressure, :reference_temperature))) :
        (; standard.reference_humidity)
    return merge(base, timed, humidity)
end

function _timed_forcings(variables::Tuple, valuesource)
    Sample = Tuple{Microclimate.TimeOfDay, Symbol}
    groups = Pair{Symbol, Vector{Sample}}[]
    for v in variables
        q = quantity(v)
        t = qualifier(q)
        t isa Microclimate.TimeOfDay || continue
        key = physical_quantity(q)
        i = findfirst(p -> first(p) === key, groups)
        sample = (t, canonical_name(v))
        i === nothing ? push!(groups, key => Sample[sample]) : push!(last(groups[i]), sample)
    end
    isempty(groups) && return (;)
    names = Tuple(first(g) for g in groups)
    forcings = map(groups) do (_, samples)
        sort!(samples; by = s -> Microclimate.nominal_hour(first(s)))
        times = Tuple(first(s) for s in samples)
        curve = _clock_curve(times)
        Microclimate.DielForcing(curve, Tuple(valuesource(last(s)) for s in samples))
    end
    return NamedTuple{names}(Tuple(forcings))
end

function _clock_curve(times::Tuple)
    n = length(times)
    n >= 2 || error("a timed forcing needs at least two samples, got $n")
    shapes = ntuple(i -> Microclimate.Linear(times[i], times[mod1(i + 1, n)]), n)
    return Microclimate.DielCurve(shapes, times)
end

@inline function _variable_buffers(variables::Tuple, n::Int)
    names = map(canonical_name, variables)
    return NamedTuple{names}(map(v -> _zeros(working_unit(quantity(v)), n), variables))
end

const _SERIES_OUTPUT = (
    Reference(Temperature()), Reference(RelativeHumidity()), WindSpeed(), Pressure(),
    CloudCover(), GlobalRadiation(), LongwaveRadiation(), Rainfall(),
    ZenithAngle(), ActualVapourPressure(),
)
const _SERIES_DAILY = (Rainfall(DailyTotal()), SoilTemperature(Mean()), SoilMoisture())

function _allocate_weather_buffers(calendar::Calendar, ::MinMax, ::Timestep, source::Type,
                                   days_of_year::AbstractVector{Int})
    nsteps = length(days_of_year)
    vars = variables(source)
    b = merge(_zero_buffers(_ENVELOPE_BUFFERS, nsteps), _variable_buffers(vars, nsteps))

    forcings = _envelope_forcings(vars, b)
    environment_minmax = _minmax_env_type(calendar)(; forcings)
    environment_daily = _environment_daily(nsteps, b.rainfall, b.deep_soil_temperature)
    environment_hourly = _environment_hourly_stub(nsteps)

    return (; b..., days_of_year,
        environment_minmax, environment_daily, environment_hourly)
end

function _allocate_weather_buffers(::Calendar, native_step::SubDaily, target_step::SubDaily,
                                   source::Type, days_of_year::AbstractVector{Int})
    native_names = _native_quantities(source)
    ndays = length(days_of_year)                  # 365 per year (no-leap calendar)
    nnat  = ndays * samples_per_day(native_step)  # native steps
    nout  = ndays * samples_per_day(target_step)  # output steps

    output = _zero_buffers(_SERIES_OUTPUT, nout)
    daily  = _zero_buffers(_SERIES_DAILY, ndays)
    native = _zero_buffers(native_names, nnat)

    environment_daily  = _environment_daily(ndays, daily.rainfall_daily, daily.deep_soil_temperature)
    environment_hourly = _environment_hourly(output)

    return (; output..., native, daily..., days_of_year,
        environment_minmax = nothing, environment_daily, environment_hourly)
end

function assemble_weather!(
    scratch, weather,
    source::Type,
    site, I::Tuple;
    grid_elevation = site.elevation,
    vapour_pressure_method = GoffGratch(),
    lapse_rate_model::LapseRate = EnvironmentalLapseRate(),
    canonical_overrides::NamedTuple = (;),
    wind_reference_height = 2.0u"m",
    target_timestep::Timestep = Hourly(),
)
    buffers = scratch.weather
    atm_pressure = atmospheric_pressure(site.elevation)
    vars = variables(source)
    cal = weather_calendar(source)
    native = native_timestep(source)

    ctx = (;
        site, grid_elevation, lapse_rate_model, vapour_pressure_method,
        atmospheric_pressure = atm_pressure, scratch,
        days_of_year = buffers.days_of_year,
        # Output day count per year, for the deep-soil annual mean.
        steps_per_year = days_per_year(cal),
        native_timestep = native, target_timestep,
        wind_reference_height,
    )

    _assemble!(native, target_timestep, buffers, weather, source, ctx, vars,
               canonical_overrides, I)
    _apply_canonical_overrides!(buffers, canonical_overrides, I)

    environment_hourly = _finalize_environment_hourly(buffers, vars, atm_pressure)

    return (; buffers.environment_minmax, buffers.environment_daily, environment_hourly)
end

@inline function _assemble!(::MinMax, ::Timestep, buffers, weather, source, ctx,
                            variables, overrides, I)
    _read_native!(buffers, weather, variables, I)
    _run_physics!(buffers, ctx, _ENVELOPE_PHYSICS, variables, overrides)
    return nothing
end
function _assemble!(native_step::SubDaily, target_step::SubDaily, buffers, weather, source,
                    ctx, variables, overrides, I)
    _read_native!(buffers.native, weather, variables, I)
    _run_physics!(buffers.native, ctx, _NATIVE_PHYSICS, variables, overrides)
    # The clear-sky solar field is an input the shortwave disaggregation reads,
    # not a derived quantity — compute it before resampling.
    _compute_solar_field!(ctx)
    _resample_to_output!(buffers, samples_per_day(native_step), samples_per_day(target_step), ctx)
    _run_physics!(buffers, ctx, _OUTPUT_PHYSICS, variables, overrides)
    return nothing
end

# TODO: this is a hack just to fill pressure.
@inline function _finalize_environment_hourly(buffers, variables, atm_pressure)
    buffers.environment_minmax === nothing && return buffers.environment_hourly
    _has_native(Val(:pressure), variables)  && return buffers.environment_hourly
    pressure = Fill(atm_pressure, length(buffers.environment_hourly.pressure))
    return setproperties(buffers.environment_hourly, (; pressure))
end

@inline function _read_native!(buffers, weather, variables::Tuple, I)
    unrolled_map(variables) do var
        _read_one_variable!(buffers, weather, var, I)
        nothing
    end
    return nothing
end

function _read_one_variable!(buffers, weather, var::Variable{Name}, I) where {Name}
    target = getproperty(buffers, Name)
    layer = getproperty(weather, Name)
    transform = var.transform
    unit = var.unit
    # Splat the spatial dim tuple from `DimIndices`; `Ti(k)` selects the
    # k-th timestep regardless of how `layer` stores its dims.
    @inbounds for k in eachindex(target)
        target[k] = transform(layer[I..., Ti(k)]) * unit
    end
    return nothing
end

function _run_physics!(buffers, ctx, chain::Tuple, variables::Tuple, overrides::NamedTuple = (;))
    unrolled_map(chain) do v
        n = canonical_name(v)
        is_available = _has_native(Val(n), variables) || _has_override(Val(n), overrides)
        is_available || derive!(v, buffers, ctx)
        nothing
    end
    return nothing
end

@inline function _apply_canonical_overrides!(buffers, overrides::NamedTuple{K}, I) where K
    unrolled_map(K) do name
        _apply_one_override!(buffers, name, getproperty(overrides, name), I)
        nothing
    end
    return nothing
end
@inline _apply_canonical_overrides!(_, ::NamedTuple{()}, _) = nothing

@inline function _apply_one_override!(buffers, name::Symbol, raster, I)
    target = getproperty(buffers, name)
    _copy_override!(target, raster, I)
    return nothing
end

@inline function _copy_override!(target::AbstractVector, raster, I)
    @inbounds if hasdim(raster, Ti)
        for k in eachindex(target)
            target[k] = raster[I..., Ti(k)]
        end
    else
        value = raster[I...]
        for k in eachindex(target)
            target[k] = value
        end
    end
    return nothing
end

@inline _has_override(::Val{N}, ::NamedTuple{K}) where {N, K} = N in K

@inline _has_native(::Val, ::Tuple{}) = false
@inline _has_native(v::Val{N}, vars::Tuple) where {N} =
    _name_matches(v, first(vars)) || _has_native(v, Base.tail(vars))

@inline _name_matches(::Val{N}, ::Variable{N}) where {N} = true
@inline _name_matches(::Val, ::Variable) = false

# ---------------------------------------------------------------------------
# Derivations
# ---------------------------------------------------------------------------

# function derive!(r::Reference, buffers, ctx)
#     derive!(r, r.sample, buffers, ctx)
# end
function derive!(r::Reference{<:Temperature}, buffers, ctx)
    _lapse_correct!(buffers[canonical_name(r)], buffers[canonical_name(r.sample)], ctx)
end
function derive!(s::Temperature{Maximum}, buffers, ctx)
    @. buffers[canonical_name(s)] =
        buffers.mean_temperature + buffers.diurnal_temperature_range / 2
    return nothing
end
function derive!(s::Temperature{Minimum}, buffers, ctx)
    @. buffers[canonical_name(s)] =
        buffers.mean_temperature - buffers.diurnal_temperature_range / 2
    return nothing
end
function derive!(s::Temperature{Mean}, buffers, ctx)
    @. buffers[canonical_name(s)] =
        (buffers.reference_temperature_max + buffers.reference_temperature_min) / 2
    return nothing
end
function derive!(s::ActualVapourPressure, buffers, ctx)
    method = ctx.vapour_pressure_method
    avp = buffers[canonical_name(s)]
    if hasproperty(buffers, :dewpoint_temperature)
        @inbounds for k in eachindex(avp)
            avp[k] = vapour_pressure(method, buffers.dewpoint_temperature[k])
        end
    elseif hasproperty(buffers, :humidity)
        @inbounds for k in eachindex(avp)
            avp[k] = buffers.humidity[k] * vapour_pressure(method, buffers.mean_temperature[k])
        end
    else
        _vapour_pressure_from_specific_humidity!(avp,
            buffers.specific_humidity, buffers.pressure)
    end
    return nothing
end
function derive!(::VapourPressureDeficit, buffers, ctx)
    method = ctx.vapour_pressure_method
    @inbounds for k in eachindex(buffers.vapour_pressure_deficit)
        saturation = vapour_pressure(method, buffers.mean_temperature[k])
        buffers.vapour_pressure_deficit[k] = saturation - buffers.actual_vapour_pressure[k]
    end
    return nothing
end
function derive!(::Reference{RelativeHumidity{Maximum}}, buffers, ctx)
    _relative_humidity_from_vpd!(buffers.reference_humidity_max,
        buffers.vapour_pressure_deficit, buffers.mean_temperature,
        buffers.reference_temperature_min, ctx.vapour_pressure_method)
    return nothing
end
function derive!(::Reference{RelativeHumidity{Minimum}}, buffers, ctx)
    _relative_humidity_from_vpd!(buffers.reference_humidity_min,
        buffers.vapour_pressure_deficit, buffers.mean_temperature,
        buffers.reference_temperature_max, ctx.vapour_pressure_method)
    return nothing
end
function derive!(::WindSpeed, buffers, ctx)
    _wind_speed!(buffers.wind_speed, buffers.eastward_wind, buffers.northward_wind)
    return nothing
end
function derive!(::Reference{WindSpeed{Maximum}}, buffers, ctx)
    shear = _wind_height_correction(ctx.wind_reference_height)
    @. buffers.reference_wind_max = buffers.wind_speed * shear
    return nothing
end
function derive!(::Reference{WindSpeed{Minimum}}, buffers, ctx)
    @. buffers.reference_wind_min = buffers.reference_wind_max * 0.1
    return nothing
end
function derive!(::CloudCover, buffers, ctx)
    (; scratch, site) = ctx
    # `flat_terrain_template` is constant except for elevation/pressure/lat/lon;
    # swap those four in via `setproperties` (zero-cost).
    flat_terrain = setproperties(scratch.cloud_constants.flat_terrain_template,
        (; site.elevation,
           atmospheric_pressure = ctx.atmospheric_pressure,
           site.latitude, site.longitude),
    )
    cloud_from_solar_radiation!(buffers.cloud_cover, scratch.solar.out,
        buffers.global_radiation, flat_terrain, buffers.days_of_year,
        scratch.solar.buffers;
        solar_model = scratch.cloud_constants.solar_model,
        hours = scratch.cloud_constants.hours,
    )
    return nothing
end
function derive!(::CloudCover{Minimum}, buffers, ctx)
    @. buffers.cloud_min = clamp(buffers.cloud_cover * 0.5, 0.0, 1.0)
    return nothing
end
function derive!(::CloudCover{Maximum}, buffers, ctx)
    @. buffers.cloud_max = clamp(buffers.cloud_cover * 2.0, 0.0, 1.0)
    return nothing
end
function derive!(::SoilTemperature{Mean}, buffers, ctx)
    _deep_soil!(buffers.deep_soil_temperature, _air_temperature(buffers), ctx.steps_per_year)
    return nothing
end
function derive!(::Rainfall{DailyTotal}, buffers, ctx)
    _resample!(buffers.rainfall_daily, buffers.native.rainfall,
               Accumulate(), samples_per_day(ctx.native_timestep))
    return nothing
end
function derive!(::Reference{WindSpeed{Nothing}}, buffers, ctx)
    shear = _wind_height_correction(ctx.wind_reference_height)
    @. buffers.wind_speed *= shear
    return nothing
end
function derive!(::Reference{RelativeHumidity{Nothing}}, buffers, ctx)
    method = ctx.vapour_pressure_method
    @inbounds for k in eachindex(buffers.reference_humidity)
        saturation = vapour_pressure(method, buffers.reference_temperature[k])
        if saturation <= zero(saturation)
            buffers.reference_humidity[k] = 1.0
        else
            buffers.reference_humidity[k] =
                clamp(buffers.actual_vapour_pressure[k] / saturation, 0.0, 1.0)
        end
    end
    return nothing
end
# Populate the flat-terrain clear-sky solar field in `scratch.solar.out`. This is
# the baseline the shortwave disaggregation divides observed radiation against; it
# is an input to the chain, produced by the solar model, not a weather quantity.
function _compute_solar_field!(ctx)
    (; scratch, site) = ctx
    flat_terrain = setproperties(scratch.cloud_constants.flat_terrain_template,
        (; site.elevation,
           atmospheric_pressure = ctx.atmospheric_pressure,
           site.latitude, site.longitude))
    solar_radiation!(scratch.solar.out, scratch.solar.buffers,
        scratch.cloud_constants.solar_model;
        solar_terrain = flat_terrain,
        days  = ctx.days_of_year,
        hours = scratch.cloud_constants.hours)
    return nothing
end

# ---------------------------------------------------------------------------
# Helpers reused by derivations
# ---------------------------------------------------------------------------

# Power-law wind height correction
_wind_height_correction(z_ref, z_src = 10.0u"m", α = 0.15) = (z_ref / z_src)^α

@inline _air_temperature(b) =
    hasproperty(b, :reference_temperature) ? b.reference_temperature : b.mean_temperature

@inline function _lapse_correct!(out, src, ctx)
    (; site, grid_elevation, lapse_rate_model) = ctx
    Δz = site.elevation - grid_elevation
    if iszero(Δz)
        @. out = src
    else
        lr = lapse_rate(lapse_rate_model)   # scalar K/m — extract before broadcast
        @. out = src - lr * Δz
    end
    return nothing
end

function _relative_humidity_from_vpd!(
    out::AbstractVector,
    vapour_pressure_deficit::AbstractVector,
    mean_temperature::AbstractVector,
    reference_temperature::AbstractVector,
    method,
)
    @inbounds for k in eachindex(out, vapour_pressure_deficit,
                                 mean_temperature, reference_temperature)
        saturation_at_mean = vapour_pressure(method, mean_temperature[k])
        actual = saturation_at_mean - vapour_pressure_deficit[k]
        if actual <= zero(actual)
            out[k] = 0.0
            continue
        end
        saturation_at_reference = vapour_pressure(method, reference_temperature[k])
        out[k] = saturation_at_reference <= zero(saturation_at_reference) ? 1.0 :
                 clamp(actual / saturation_at_reference, 0.0, 1.0)
    end
    return out
end

# ---------------------------------------------------------------------------
# Date-range helpers
# ---------------------------------------------------------------------------

@inline _minmax_env_type(::Monthly) = MonthlyMinMaxEnvironment
@inline _minmax_env_type(::Daily) = DailyMinMaxEnvironment

@inline _days_of_year(::Monthly, nyears) = repeat(Microclimate.DEFAULT_DAYS, nyears)
@inline _days_of_year(::Daily, nyears) = repeat(1:365, nyears)

# Convert an old-style integer years range to a full-calendar Date range.
_years_to_dates(years::AbstractRange{Int}) =
    Date(first(years), 1, 1):Day(1):Date(last(years), 12, 31)

# Derive the integer year span that must be loaded from weather sources to
# cover all dates.
_years_from_dates(d::Date) = year(d):year(d)
_years_from_dates(r::AbstractRange{Date})  = minimum(year, r):maximum(year, r)

# Day-of-year on the 365-day calendar (Feb 29 is dropped, so March 1 = doy 60
# whether or not the year is a leap year).
function _doy_noleap(d::Date)
    doy = dayofyear(d)
    isleapyear(year(d)) && d >= Date(year(d), 3, 1) && (doy -= 1)
    return doy
end

_is_leapday(d::Date) = isleapyear(year(d)) && month(d) == 2 && day(d) == 29

# Normalise user-supplied dates to a sorted Vector{Date}, dropping Feb 29
# (consistent with the 365-day-per-year convention throughout).
_normalise_dates(d::Date) = _is_leapday(d) ? Date[] : [d]
function _normalise_dates(r::AbstractRange{Date})
    v = filter(!_is_leapday, collect(r))
    isempty(v) && error(
        "No valid simulation dates remain after dropping Feb 29 " *
        "(leap days are not yet supported).")
    return v
end

function _ti_range_for_dates(::Monthly, ::Timestep, years, dates_vec::Vector{Date})
    start_d, end_d = minimum(dates_vec), maximum(dates_vec)
    years_v = collect(years)
    yi_start = findfirst(==(year(start_d)), years_v)
    yi_end   = findfirst(==(year(end_d)),   years_v)
    ti_start = (yi_start - 1) * 12 + month(start_d)
    ti_end   = (yi_end   - 1) * 12 + month(end_d)
    days_doy = Int[]
    d = Date(year(start_d), month(start_d), 1)
    stop = Date(year(end_d), month(end_d), 1)
    while d <= stop
        push!(days_doy, Microclimate.DEFAULT_DAYS[month(d)])
        d += Month(1)
    end
    return ti_start, ti_end, days_doy
end
function _ti_range_for_dates(::Daily, native::Timestep, years, dates_vec::Vector{Date})
    spd = samples_per_day(native)
    start_d, end_d = minimum(dates_vec), maximum(dates_vec)
    years_v = collect(years)
    yi_start = findfirst(==(year(start_d)), years_v)
    yi_end   = findfirst(==(year(end_d)),   years_v)
    ti_start = (yi_start - 1) * 365 * spd + (_doy_noleap(start_d) - 1) * spd + 1
    ti_end   = (yi_end   - 1) * 365 * spd + _doy_noleap(end_d) * spd
    return ti_start, ti_end, _doy_noleap.(dates_vec)
end

# One "anchor" Date per solver step — used to carry the year when building
# the output DateTime axis. Monthly: 1st of each selected month. Daily: the
# dates_vec itself (already one per calendar day).
function _step_anchor_dates(::Monthly, dates_vec::Vector{Date})
    seen = Set{Tuple{Int,Int}}()
    result = Date[]
    for d in sort(dates_vec)
        k = (year(d), month(d))
        k in seen && continue
        push!(seen, k)
        push!(result, Date(year(d), month(d), 1))
    end
    return result
end
_step_anchor_dates(::Daily, dates_vec::Vector{Date}) = dates_vec

# Monthly forcing → independent representative days (Fortran "monthly mode");
# Daily forcing → continuous run with state carrying day-to-day.
@inline _time_mode(::Monthly) = Microclimate.NonConsecutiveDayMode()
@inline _time_mode(::Daily) = Microclimate.ConsecutiveDayMode()

